#!/usr/local/bin/ot

//	ObjectTalk Scripting Language
//	Copyright (c) 1993-2024 Johan A. Goossens. All rights reserved.
//
//	This work is licensed under the terms of the MIT license.
//	For a copy, see <https://opensource.org/licenses/MIT>.

var gui = import("gui");

class MyApp : gui.App {
	function setup(this) {
		var columns = gui.Columns([25, 50, 25]);
		this.add(columns);

		var rows = gui.Rows([45, 55]);
		rows.add(this.tronPanel());
		rows.add(this.propertiesPanel());
		columns.add(rows);

		columns.add(gui.Header("Column 2"));
		columns.add(gui.Header("Column 3"));
	}

	function tronPanel(this) {
		var panel = gui.Panel();
		panel.add(gui.Tron());
		return panel;
	}

	function propertiesPanel(this) {
		var properties = gui.Panel();
		var widgets = gui.TreeNode("Property Widgets");
		properties.add(widgets);

		widgets.add(gui.Checkbox("Boolean", false, function(checked) {
			print("Boolean: ", checked);
		}));

		widgets.add(gui.IntegerSlider("Integer", 50, 0, 100, function(value) {
			print("Integer: ", value);
		}));

		widgets.add(gui.RealSlider("Real", 0.5, 0.0, 1.0, function(value) {
			print("Real: ", value);
		}));

		widgets.add(gui.TextField("String", "", function(value) {
			print("String: ", value);
		}));

		widgets.add(gui.Combobox("Selector", "", ["", "one", "two", "three"], function(value) {
			print("Selector: ", value);
		}));

		return properties;
	}
}

var app = MyApp();
app.run();
