#!/usr/local/bin/ot

//	ObjectTalk Scripting Language
//	Copyright (c) 1993-2024 Johan A. Goossens. All rights reserved.
//
//	This work is licensed under the terms of the MIT license.
//	For a copy, see <https://opensource.org/licenses/MIT>.

var gui = import("gui");

class MyApp : gui.App {
	function setup(this) {
		var hsplitter = gui.Hsplit(0.25, 0.2, 0.8);
		this.add(hsplitter);

		var vsplitter = gui.Vsplit(0.45, 0.3, 0.6);
		hsplitter.add(vsplitter);

		var topPanel = gui.Panel();
		vsplitter.add(topPanel);
		topPanel.add(gui.Header("Tron Clock"));
		topPanel.add(gui.Tron());

		var bottomPanel = gui.Panel();
		vsplitter.add(bottomPanel);

		var widgets = gui.TreeNode("Panel Widgets");
		bottomPanel.add(widgets);
		
		widgets.add(gui.Checkbox("Boolean", false, function(checked) {
			print("Boolean: ", checked);
		}));
		
		widgets.add(gui.IntegerSlider("Integer", 50, 0, 100, function(value) {
			print("Integer: ", value);
		}));

		widgets.add(gui.RealSlider("Real", 0.5, 0.0, 1.0, function(value) {
			print("Real: ", value);
		}));

		widgets.add(gui.TextField("String", "", function(value) {
			print("String: ", value);
		}));

		widgets.add(gui.Combobox("Selector", "", ["", "one", "two", "three"], function(value) {
			print("Selector: ", value);
		}));
	}
}

var app = MyApp();
app.run();
